// Copyright 2019 Axel Howind
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

plugins {
    id 'com.adarshr.test-logger' version '2.1.0' apply false
    id 'com.github.spotbugs' version '4.7.0' apply false
    id "org.openjfx.javafxplugin" version '0.0.9' apply false
}

subprojects {
    ext.fast_build   = project.findProperty("fast_build")!=null
    ext.run_spotbugs = !fast_build

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.openjfx.javafxplugin'
    apply plugin: 'maven-publish'
    apply plugin: 'idea'
    apply plugin: 'com.adarshr.test-logger'

    /////////////////////////////////////////////////////////////////////////////
    group = 'com.dua3.fx'
    version = '0.15-beta1'

    ext.moduleName = project.group
    ext.scm = 'https://gitlab.com/com.dua3/lib/fx.git'
    ext.repo = 'public'
    ext.description = 'Java FX application utilities'

    ext.developerId = 'axh'
    ext.developerName = 'Axel Howind'
    ext.developerEmail = 'axel@dua3.com'
    ////////////////////////////////////////////////////////////////////////////

    ext {
        java_version = '11'
        javafx_version = '16'
        junit_version = '5.5.1'

        dua3_utility_version = '9-beta1'

        ikonli_version = '12.2.0'
    }

    sourceCompatibility = java_version
    targetCompatibility = java_version

    // repositories
    repositories {
        // local Maven repository
        mavenLocal()
        
        // GitLab Packages
        maven {
            url "https://gitlab.com/api/v4/projects/24900343/packages/maven"
            name "GitLab"
            credentials(HttpHeaderCredentials) {
                name = 'Private-Token'
                value = gitLabPrivateToken
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }

        mavenCentral()
    }

    javafx {
        version = "${javafx_version}"
        configuration = 'compileOnly'
    }
    
    // dependencies
    dependencies {
        // JUnit
        testImplementation "org.junit.jupiter:junit-jupiter-api:${junit_version}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junit_version}"
    }

    idea {
        module {
            inheritOutputDirs = false
            outputDir = file("$buildDir/classes/java/main/")
            testOutputDir = file("$buildDir/classes/java/test/")
        }
    }

    test {
        useJUnitPlatform()
    }

    testlogger {
        theme 'standard-parallel'
    }

    javadoc {
        options.encoding = 'UTF-8'
    }

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }

    // === ARTIFACTS === >
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives jar

        if (!fast_build) {
            archives sourcesJar
            archives javadocJar
        }
    }

    // === MAVEN publication === >

    // Create the pom configuration:
    def pomConfig = {
        licenses {
            license {
                name "The Apache Software License, Version 2.0"
                url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id project.developerId
                name project.developerName
                email project.developerEmail
            }
        }

        scm {
            url project.scm
        }
    }

    // Create the publication with the pom configuration:
    publishing {
        publications {
            projectPublication(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                groupId project.group
                artifactId project.name
                version version
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', project.description)
                    root.appendNode('name', project.group)
                    root.appendNode('url', project.scm)
                    root.children().last() + pomConfig
                }
            }
        }

        repositories {
            maven {
                url "https://gitlab.com/api/v4/projects/24900343/packages/maven"
                credentials(HttpHeaderCredentials) {
                    name = "Private-Token"
                    value = gitLabPrivateToken // the variable resides in ~/.gradle/gradle.properties
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        }
    }

    // === SPOTBUGS === >
    if (run_spotbugs) {
        apply plugin: 'com.github.spotbugs'

        tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
            reports {
                xml.enabled false
                html.enabled true
            }
        }
    }
    
}

tasks.withType(PublishToMavenRepository) {
    dependsOn publishToMavenLocal
}

// === DEFAULT TASKS === >
defaultTasks 'build', 'publishToMavenLocal'
